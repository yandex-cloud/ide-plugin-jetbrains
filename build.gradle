buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.intellij' version '1.15.0'
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group 'ru.yandex'
version pluginVersion

allprojects {
    repositories {
        mavenCentral()
    }
}

buildSearchableOptions.enabled = false

dependencies {
    implementation "com.yandex.cloud:java-sdk-services:$YC_SDK_VERSION"
    implementation "com.yandex.cloud:java-sdk-functions:$YC_SDK_VERSION"
    implementation "com.yandex.cloud:java-sdk-auth:$YC_SDK_VERSION"

    implementation "com.google.protobuf:protobuf-java:$PROTOBUF_VERSION"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '17'

        freeCompilerArgs += [
                // This flag is required due to the current implementation using
                // the coroutine builder [CoroutineScope.actor]. This can be removed
                // once the coroutines stdlib is updated with a suitable replacement
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"
        ]
    }
}

jar {
    archiveClassifier.set('raw')
}

shadowJar {
    archiveClassifier.set(null)
    mergeServiceFiles()
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = ideaVersion
}

patchPluginXml {
    sinceBuild = minIDEVersion
    untilBuild = null

    copy {
        from changesFile
        into "build/changelog"
    }

//    markdownToHtml {
//        sourceDir file("build/changelog")
//        outputDir file("build/changelog")
//    }.runTask()
//
//    pluginDescription = file(descriptionFile).text
//    changeNotes = file("build/changelog/CHANGELOG.html").text
}

configurations.all {
    exclude group:"io.grpc", module: "grpc-api"
    exclude group:"io.grpc", module: "grpc-core"
    exclude group:"io.grpc", module: "grpc-netty-shaded"
}

task printVersion {
    doLast {
        logger.lifecycle("::set-output name=plugin-version::" + project.version)
    }
}

buildPlugin.dependsOn(shadowJar)

task buildPluginRepository(type: Copy) {
    from './resources/pluginRepository.xml'
    into buildDir
    rename { "updatePlugins.xml" }
    expand("version": version, "sincebuild": minIDEVersion)
}

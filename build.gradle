import org.jetbrains.changelog.Changelog

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.intellij.platform' version '2.2.1'
    id 'org.jetbrains.kotlin.jvm' version '2.1.10'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'org.jetbrains.changelog' version '2.2.1'
}

repositories {
    intellijPlatform {
        defaultRepositories()
    }
    maven {
        url = 'https://maven.atlassian.com/content/repositories/atlassian-public/'
    }
}

group = 'ru.yandex'
version = pluginVersion

allprojects {
    repositories {
        mavenCentral()
    }
}

buildSearchableOptions.enabled = false

dependencies {
    implementation "com.yandex.cloud:java-sdk-services:$YC_SDK_VERSION"
    implementation "com.yandex.cloud:java-sdk-functions:$YC_SDK_VERSION"
    implementation "com.yandex.cloud:java-sdk-auth:$YC_SDK_VERSION"

    implementation "com.google.protobuf:protobuf-java:$PROTOBUF_VERSION"
    implementation 'com.michaelbaranov:microba:0.4.4.3'

    intellijPlatform {
        intellijIdeaCommunity ideaVersion
        bundledPlugin 'com.intellij.modules.json'
    }
}

java {
    sourceCompatibility = '21'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '21'
    }
}

jar {
    archiveClassifier.set('raw')
}

shadowJar {
    archiveClassifier.set(null)
    mergeServiceFiles()
    dependsOn(jarSearchableOptions)
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

intellijPlatform {
    pluginConfiguration {
        description = file(descriptionFile).text

        changeNotes = project.changelog.with {
            renderItem(
                    (getOrNull(pluginVersion) ?: getUnreleased())
                            .withHeader(false)
                            .withEmptySections(false),
                    Changelog.OutputType.HTML
            )
        }
    }
}

patchPluginXml {
    sinceBuild = minIDEVersion
    untilBuild = maxIDEVersion

    copy {
        from changesFile
        into "build/changelog"
    }
}

task printVersion {
    doLast {
        logger.lifecycle("::set-output name=plugin-version::" + project.version)
    }
}

buildPlugin.dependsOn(shadowJar)

task buildPluginRepository(type: Copy) {
    from './resources/pluginRepository.xml'
    into buildDir
    rename { "updatePlugins.xml" }
    expand("version": version, "sincebuild": minIDEVersion)
}
